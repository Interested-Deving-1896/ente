plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.10'
}
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def userPropertiesFile = file("${System.getProperty('user.home')}/Documents/up_dev_signature/signing.properties")
def userProperties = new java.util.Properties()
if (userPropertiesFile.exists()) {
    userProperties.load(userPropertiesFile.newInputStream())
}

android {
    namespace = "io.ente.photos"
    compileSdk = 35
//    ndkVersion = flutter.ndkVersion
    ndkVersion = "27.0.12077973"

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
        warningsAsErrors false
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId = "com.unplugged.photos"
        minSdk = 26
        targetSdk = 35
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        // consumerProguardFiles 'proguard-rules.pro' // Commented out for debug builds - uncomment for release optimization

    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    signingConfigs {
        // Debug signing with local keystore
        def keystoreFile = file("${System.getProperty('user.home')}/Documents/up_dev_signature/up_dev.keystore")
        if (keystoreFile.exists()) {
            debug {
                storeFile keystoreFile
                storePassword userProperties.getProperty('keystorePassword', '')
                keyAlias userProperties.getProperty('keyAlias', '')
                keyPassword userProperties.getProperty('keyPassword', '')
            }
        }
        
        // Release signing via Jenkins externally
        release {
            storeFile userProperties['storeFile'] ? file(userProperties['storeFile']) : System.getenv("SIGNING_KEY_PATH") ? file(System.getenv("SIGNING_KEY_PATH")) : null
            keyAlias userProperties['keyAlias'] ? userProperties['keyAlias'] : System.getenv("SIGNING_KEY_ALIAS")
            keyPassword userProperties['keyPassword'] ? userProperties['keyPassword'] : System.getenv("SIGNING_KEY_PASSWORD")
            storePassword userProperties['storePassword'] ? userProperties['storePassword'] : System.getenv("SIGNING_STORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix ".dev"
            versionNameSuffix "-debug"
            externalNativeBuild {
                cmake {
                    arguments "-DANDROID_PACKAGE_NAME=${android.defaultConfig.applicationId}${applicationIdSuffix}"
                }
            }
        }
    }
}

rootProject.allprojects {
    subprojects {
        project.configurations.all {
            resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'com.github.bumptech.glide'
                        && details.requested.name.contains('glide')) {
                    details.useVersion "4.15.1"
                }
            }
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.21'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    implementation 'com.microsoft.onnxruntime:onnxruntime-android:1.18.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.4")

    constraints {
        implementation("androidx.work:work-runtime:2.8.1") {
            because("Align work-runtime versions")
        }
        implementation("androidx.work:work-runtime-ktx:2.8.1") {
            because("Align work-runtime-ktx versions")
        }
    }
}
